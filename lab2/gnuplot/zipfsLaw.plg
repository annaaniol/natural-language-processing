set terminal svg
set datafile separator ','
set output outputname
set xrange [1:maxRankNumber]

set title "Zipf's law"
set xlabel "Ranking possition"
set ylabel "Occurrences"

set logscale y

k = 43000

zipf(r) = k/r

P = 45000
d = 0.1
B = 1.02

mandelbrot(r) = P / ((r+d)**B)

zipfTitle(k) = sprintf('Zipf k=%d',k)
mandelbrotTitle(P,d,B) = sprintf('Mandelbrot P=%d d=%f B =%f', P, d, B)

plot datafile using 1:($1 < maxRankNumber ? $2 : 1/0) title 'Measured values' with lines, \
     datafile using 1:($1 < maxRankNumber ? zipf($1) : 1/0) title zipfTitle(k) with lines, \
     datafile using 1:($1 < maxRankNumber ? mandelbrot($1) : 1/0) title mandelbrotTitle(P,d,B) with lines, \
